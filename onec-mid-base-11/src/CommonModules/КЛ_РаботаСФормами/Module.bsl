#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы; 
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьСкидкуНаФормуВЗаказПокупателя(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьКонтактноеЛицоПередОснование(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода; 
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьКонтактноеЛицоПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода; 
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьСкидкуНаФормуВЗаказПокупателя(Форма)

	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеСкидка = Форма.Элементы.Добавить("Скидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидка.ПутьКДанным = "Объект.КЛ_СогласованнаяСкидка";
	ПолеСкидка.УстановитьДействие("ПриИзменении", "ПриИзмененииСкидки");
	
	КомандаПересчитать = Форма.Команды.Добавить("КомандаПересчитатьСкидку");
	КомандаПересчитать.Действие = "ПересчитатьСкидку";
	
	КнопкаПересчитать = Форма.Элементы.Добавить("КнопкаПересчитатьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаПересчитать.Заголовок = "Пересчитать скидку";
	КнопкаПересчитать.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаПересчитать.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаПересчитать.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаПересчитать.ИмяКоманды = "КомандаПересчитатьСкидку"; 

КонецПроцедуры

#КонецОбласти